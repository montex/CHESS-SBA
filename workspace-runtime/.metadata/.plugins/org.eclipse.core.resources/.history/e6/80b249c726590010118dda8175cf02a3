library DEEM2String;

helper context DEEM!Net def: toString():String = 
	'<NDFile-\'1.2\'>\n\n'+
	'NET:"'+self.Name+'",2000,2000;\n'+
	'COLORS:White,Black,Black,Black,Black,Black;\n'+
	self.places->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.transitions->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.inputArcs->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.outputArcs->iterate(i; acc:String=''| acc + i.toString());
	
helper context DEEM!Place def: toString(): String = 
	'PLACE:"'+self.Name+'",0,0,'+self.marking+',0;\n';
	
helper context DEEM!ImmediateTransition def: toString(): String = 
	'TRANS:"'+self.Name+'",0,0,V,Immediate,'+self.Priority+',0,\'\', \'\',\n'+
    ' (\n'+
    '  '+self.params->iterate(i; acc : String = '' |
		acc +
		if acc = '' then
			''
		else
			',\n'
		endif +
		i.toString())+'\n'+
    '  );\n';
    
helper context DEEM!TransitionType def: toString(): String =
	if self = #Deterministic then
		'0'
	else if self=#Exponential then
		'1'
	else if self=#Normal then
		'11'
	else if self=#Uniform then
		'13'
	else if self=#Gamma then
		'3'
	else --if self=#Weibull then
		'14'
	endif endif endif endif endif;

helper context DEEM!TimedTransition def: toString(): String =
	'TRANS:"'+self.Name+'",0,0,V,Timed,1,'+self.type.toString()+',\'\', \'\',\n'+
    ' (\n'+
    '  '+self.params->iterate(i; acc : String = '' |
		acc +
		if acc = '' then
			''
		else
			',\n'
		endif +
		i.toString())+'\n'+
    '  );\n';

helper context DEEM!ArcType def: toString(): String =
	if self = #Normal then
		'0'
	else 
		'2'
	endif;

helper context DEEM!InputArc def: toString(): String =
	'IARC:"'+self.source.Name+'","'+self.target.Name+'",1,'+self.type.toString()+',\'\'\n'+
    ' (0,0,0,0),1;\n';
    
helper context DEEM!OutputArc def: toString(): String =
	'OARC:"'+self.source.Name+'","'+self.target.Name+'",1,1,\'\'\n'+
    ' (0,0,0,0),1;\n';

