-- @path DEEM=CHESSReviewDemo/metamodels/deem.ecore

library DEEM2String;

helper context DEEM!Net def: toString():String = 
	'<NDFile-\'1.2\'>\n\n'+
	'NET:"'+self.Name+'",2000,2000;\n'+
	'COLORS:White,Black,Black,Black,Black,Black;\n\n'+
	'VARIABLE:"Time","Real";\n\n'+
	'STUDY:"DefaultStudy",0,"not_def",10000;\n\n'+
	self.places->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.transitions->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.inputArcs->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	self.outputArcs->iterate(i; acc:String=''| acc + i.toString())+
	'\n\n'+
	'RES_FUNC:"pFail",\'IF(MARK(failureModePlace_c1_c1_fm16)>0)THEN(1)ELSE(0)\',0;'+
	'\n';
	
helper context DEEM!Place def: toString(): String = 
	'PLACE:"'+self.Name+'",1500,1500,'+self.marking+',1;\n';
	
helper context DEEM!ImmediateTransition def: toString(): String = 
--	'TRANS:"'+self.Name+'",1500,1500,V,Immediate,'+self.Priority+',0,\'\', \'\',\n'+
    'TRANS:"'+self.Name+'",1500,1500,V,Immediate,1,1,\'\', \''+self.params->first()+'\',\n'+
    ' (\n'+
    '1.000000e+00\n' +
    '  '+self.params->subSequence(2, self.params->size())->iterate(i; acc : String = '' |
			acc +
			if acc = '' then
				''
			else
				',\n'
			endif +
			'\''+i.toString()+'\''
		)+
	'\n'+
    '  );\n';
    
helper context DEEM!TimedTransition def: toString(): String =
	'TRANS:"'+self.Name+'",1500,1500,V,Timed,1,'+
	if self.type = #Deterministic then
		'0'
	else if self.type=#Exponential then
		'1'
	else if self.type=#Normal then
		'11'
	else if self.type=#Uniform then
		'13'
	else if self.type=#Gamma then
		'3'
	else --if self=#Weibull then
		'14'
	endif endif endif endif endif
	+',\'\', \''+self.params->first()+'\',\n'+
    ' (\n'+
    '1.000000e+00\n' +
    '  '+self.params->subSequence(2, self.params->size())->iterate(i; acc : String = '' |
		acc +
		if acc = '' then
			''
		else
			',\n'
		endif +
	    '\''+i.toString()+'\''
	)+'\n'+
    '  );\n';

--helper context DEEM!ArcType def: toString(): String =
--	if self = #Normal then
--		'0'
--	else 
--		'2'
--	endif;

helper context DEEM!InputArc def: toString(): String =
	'IARC:"'+self.source.Name+'","'+self.target.Name+'",1,0,\'\',\n'+
    ' (1500,1500,1500,1500),1;\n';
    
helper context DEEM!OutputArc def: toString(): String =
	'OARC:"'+
    if self.oclIsTypeOf(DEEM!InhibitorArc) then
		self.source.Name+'","'+self.target.Name+'",1,2'
	else
		self.target.Name+'","'+self.source.Name+'",1,1'
	endif
	+',\'\',\n'+
    ' (1500,1500,1500,1500),1;\n';

