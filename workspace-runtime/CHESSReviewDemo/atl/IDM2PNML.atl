-- @path IDM=/CHESSReviewDemo/metamodels/IM2.ecore
-- @path PNML=/CHESSReviewDemo/metamodels/placeTransition.ecore

module IDM2PNML;
create OUT : PNML from IN : IDM;

helper def: trCount : Integer = 1;
helper def: arcCount : Integer = 1;
helper def: timePoints : OrderedSet(Real) = OrderedSet{};

helper def: getPage : PNML!Page = IDM!Sistema.allInstances()->first();
	
rule Sistema {
	from
		s : IDM!Sistema
	to
		pp: PNML!Page (
			id <- s.Name,
			objects <- Sequence{}
		),
		net: PNML!PetriNet (
			id <-  s.Name,
			pages <- Sequence{pp}
		),
		doc: PNML!PetriNetDoc (
			nets <- Sequence{net}
		),
		
		var : PNML!Variable (
			name <- 'Time'
		),
		varvals : PNML!VariableValues (
			variable <- var,
			values <- OrderedSet{}
		),
		study: PNML!Study (
			name <- 'DefaultStudy',
			vars <- Sequence{varvals}
		),
		evalList: PNML!EvaluationList (
			studies <- Sequence{study},
			measures <- PNML!Measure.allInstances()
		)
	do {
		for(i in IDM!InstantOfTime.allInstances()) {
			varvals.values <- varvals.values.append(i.timePoint);
		}
	}
}

-- Crea le transizioni basandomi sulle distribuzioni utilizzate
rule ImmediateTransition {
	from
		dist : IDM!Deterministic
		(
			dist.Value = 0
		)
	to
		tr : PNML!GSPNImmediateTransition (
			Priority <- 1,
			containerPage <- thisModule.getPage
		)
}
rule DeterministicTransition {
	from
		dist : IDM!Deterministic
		(
			dist.Value > 0
		)
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- d,
			containerPage <- thisModule.getPage
		),
		d : PNML!Deterministic (
			Value <- dist.Value
		)
}
abstract rule TimedTransition {
	from
		dist : IDM!Distribution
		(
			not dist.oclIsTypeOf(IDM!Deterministic)	
		)
	to
		tr : PNML!GSPNTimedTransition (
			containerPage <- thisModule.getPage	
		)
}
rule ExponentialTransition extends TimedTransition {
	from
		dist: IDM!Exponential
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- ee
		),
		ee : PNML!Exponential (
			Rate <- dist.Rate 
		)
}
rule GaussianTransition extends TimedTransition {
	from
		dist : IDM!Gaussian
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- gg
		),
		gg : PNML!Gaussian (
			Mean <- dist.Mean,
			Variance <- dist.Variance
		)
}
rule UniformTransition extends TimedTransition {
	from
		dist : IDM!Uniform
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- uu
		),
		uu : PNML!Uniform (
			Lower <- dist.Lower,
			Upper <- dist.Upper
		)
}
rule GammaTransition extends TimedTransition {
	from
		dist : IDM!Gamma
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- gg
		),
		gg : PNML!Gamma (
			Alpha <- dist.Alpha,
			Beta <- dist.Beta
		)
}
rule WeibullTransition extends TimedTransition {
	from
		dist : IDM!Weibull
	to
		tr : PNML!GSPNTimedTransition (
			Distribution <- ww
		),
		ww : PNML!Weibull (
			Alpha <- dist.Alpha,
			Beta <- dist.Beta
		)
}

rule InternalFault {
	from 
		ft : IDM!InternalFault
	to
		faulty : PNML!Place (
			containerPage <- thisModule.getPage,
			initialMarking <- thisModule.getPTMarking(0),
			id <- ft.Name
		),
		transient : PNML!Place (
			containerPage <- thisModule.getPage,
			initialMarking <- thisModule.getPTMarking(0),
			id <- ft.Name + '_transient'	
		)
		
}

lazy rule getPTMarking {
	from
		v : Integer
	to
		m : PNML!PTMarking (
			text <- v
		)
}

